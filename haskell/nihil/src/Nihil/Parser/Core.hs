module Nihil.Parser.Core
  ( LabelledParser
  , KeyedParser
  , ParserState (..)
  , ParserContext (..)
  , Parser
  , StopOnTiming (..)
  , BlockMaker
  , keyedAppend
  , reportError
  , throwError
  , reportExpectedError
  , spanned
  , token
  , parseTest
  , alsoStopOn
  , alsoStopOnPre
  , alsoStopOnPost
  , resetStopOn
  , withIndentation
  , exactlyIndented
  , anyIndentation
  , unindented
  , mkBlock
  , tryMkBlock
  , blockLike
  , badIndent
  , saveTrivia
  , checkIndentation
  , sc
  , label
  , junkTill
  , tryJunkTill
  , tryReportedJunkTill
  , noHints
  ) where

import Relude

import Data.Char qualified as Char
import Data.Foldable1 (foldl1)
import Data.Sequence ((|>))
import Data.Sequence qualified as Seq
import Data.Set qualified as Set
import Data.Text qualified as Text
import Error.Diagnose qualified as DG
import Nihil.Cst.Base qualified as Base
import Nihil.Error qualified as Error
import Nihil.Utils (textPretty)
import Optics ((%))
import Optics qualified as O
import Prettyprinter qualified as PP
import Text.Megaparsec qualified as M
import Text.Megaparsec.Char qualified as M
import Text.Megaparsec.Internal qualified as MI
import Text.Megaparsec.State qualified as M

---------- The parser monad
data IndentationRelation
  = IGT
  | IGTE
  | IEQ
  | IAny
  deriving (Generic, Show)

data ParserState
  = ParserState
  { trivia ‚à∑ Seq Base.Trivia
  -- ^ trivia that have been parsed, but are yet to get attached to some token.
  , relation ‚à∑ IndentationRelation
  }
  deriving (Generic, Show)

data ParserContext
  = ParserContext
  { indentation ‚à∑ M.Pos
  -- ^ All tokens must start at this column (or to the right)
  , stopOn ‚à∑ (KeyedParser (), KeyedParser ())
  -- ^ When consuming junk input, stop when this parser succeeds.
  --
  -- The first element of the pair runs before the parser we're currently
  -- actively working on, and the one on the right runs after.
  }
  deriving (Generic)

-- | Tells functions like @'alsoStopOn' whether to run the parser before the
-- thing we're currently parsing, or after.
data StopOnTiming = Pre | Post

-- | Wrapper around @'Error.Report', as it doesn't have an @'Ord' instance.
data Error = Error Int Error.Report

-- NOTE: this instance is not correct, but I added it nonetheless in order to
-- satisfy the compiler üò≠ (why doesn't @'Error.Report' have one???).
instance Eq Error where
  (Error a _) == (Error b _) = a == b

instance Ord Error where
  compare (Error a _) (Error b _) = compare a b

instance M.ShowErrorComponent Error where
  showErrorComponent (Error _ r) =
    Text.unpack
      . Error.showDiagnostic
      . Error.addReports
      . pure
      $ r

type Parser =
  ReaderT
    ParserContext
    (StateT ParserState (M.Parsec Error Text))

-- | Save a piece of trivia to the parser state. The piece will later on get
-- associated with the next token, and kept inside the CST.
saveTrivia ‚à∑ Base.Trivia ‚Üí Parser ()
saveTrivia c = O.modifying #trivia (|> c)
{-# INLINE saveTrivia #-}

-- | Update the minimum indentation of some parser.
withIndentation ‚à∑ ‚àÄ a. M.Pos ‚Üí Parser a ‚Üí Parser a
withIndentation level = local (O.set #indentation level)
{-# INLINE withIndentation #-}

-- | Tell a parser to stop consuming junk when hitting a certain element.
alsoStopOn ‚à∑ ‚àÄ a. (LabelledParser (), LabelledParser ()) ‚Üí Parser a ‚Üí Parser a
alsoStopOn (pre, post) = alsoStopOnPre pre . alsoStopOnPost post
{-# INLINE alsoStopOn #-}

-- | Tell a parser to stop consuming junk when hitting a certain element.
--
-- The given parser runs before the one we're actively trying to complete.
alsoStopOnPre ‚à∑ ‚àÄ stop a. LabelledParser stop ‚Üí Parser a ‚Üí Parser a
alsoStopOnPre pre = local $ O.over (#stopOn % O._1) (keyedAppend . fmap void $ pre)
{-# INLINE alsoStopOnPre #-}

-- | Tell a parser to stop consuming junk when hitting a certain element.
--
-- The given parser runs before the one we're actively trying to complete.
alsoStopOnPost ‚à∑ ‚àÄ stop a. LabelledParser stop ‚Üí Parser a ‚Üí Parser a
alsoStopOnPost post = local $ O.over (#stopOn % O._2) (keyedAppend . fmap void $ post)
{-# INLINE alsoStopOnPost #-}

-- | Tells a parser to only stop on bad indentation & EOF.
resetStopOn ‚à∑ ‚àÄ a. Parser a ‚Üí Parser a
resetStopOn =
  local $
    O.set
      #stopOn
      (KeyedParser mempty badIndent, KeyedParser mempty M.eof)
{-# INLINE resetStopOn #-}

-- | Create a custom error. The offset is there for sorting purposes.
reportError
  ‚à∑ Text
  ‚Üí Error.Doc
  ‚Üí [(Error.Span, DG.Marker Error.Doc)]
  ‚Üí [DG.Note Error.Doc]
  ‚Üí Parser ()
reportError code desc markers hints = do
  offset ‚Üê M.getOffset
  M.registerFancyFailure
    . Set.singleton
    . M.ErrorCustom
    $ Error offset
    $ DG.Err (Just . PP.pretty $ "Parser" <> code) desc markers hints

throwError
  ‚à∑ Int
  ‚Üí Text
  ‚Üí Error.Doc
  ‚Üí [(Error.Span, DG.Marker Error.Doc)]
  ‚Üí [DG.Note Error.Doc]
  ‚Üí Parser ()
throwError offset code desc markers hints =
  M.region (M.setErrorOffset offset)
    . M.registerFancyFailure
    . Set.singleton
    . M.ErrorCustom
    $ Error offset
    $ DG.Err (Just . PP.pretty $ "Parser" <> code) desc markers hints

-- | Attempt to run a parser on some string, reporting the results to stdout.
-- An error is reported if the parser does not consume the full string it's
-- given. The parser state at the very end of the parse is also shown.
parseTest ‚à∑ ‚àÄ a. (PP.Pretty a) ‚áí Parser a ‚Üí Text ‚Üí IO ()
parseTest p input = do
  let initialCtx =
        ParserContext
          (M.mkPos 1)
          ( KeyedParser mempty empty
          , KeyedParser mempty empty
          )

  let initialState = ParserState mempty IAny

  let filename = "<test>"
  let readerResult = runReaderT (resetStopOn $ sc *> p <* M.eof) initialCtx
  let stateResult = runStateT readerResult initialState
  let (errs, result) = runParser stateResult filename input

  for_ result \(x, finalState) ‚Üí do
    putTextLn $ textPretty finalState
    putTextLn $ textPretty x

  Error.printDiagnostic $
    DG.addFile
      (Error.addReports errs)
      filename
      (Text.unpack input)

-- | Forget every hint collected by megaparsec.
--
-- Used for performance only.
noHints ‚à∑ ‚àÄ a. Parser a ‚Üí Parser a
noHints p = ReaderT \ctx ‚Üí StateT \s ‚Üí
  MI.ParsecT \ps cok cerr eok eerr ‚Üí
    MI.unParser
      (runStateT (runReaderT p ctx) s)
      ps
      (\a st _ ‚Üí cok a st mempty)
      (\pe st ‚Üí cerr pe st)
      (\a st _ ‚Üí eok a st mempty)
      (\pe st ‚Üí eerr pe st)
{-# INLINE noHints #-}

runParser
  ‚à∑ ‚àÄ a
   . M.Parsec Error Text a
  ‚Üí String
  ‚Üí Text
  ‚Üí ([Error.Report], Maybe a)
runParser p s i = runIdentity do
  (MI.Reply s' _ result) ‚Üê MI.runParsecT p (M.initialState s i)
  -- TODO: take custom error spans into account when sorting
  let bundle es = finalizeError =<< sortWith M.errorOffset es
  pure $ case result of
    MI.OK _ x ‚Üí (bundle (M.stateParseErrors s'), Just x)
    MI.Error e ‚Üí (bundle $ e : M.stateParseErrors s', Nothing)
 where
  finalizeError ‚à∑ M.ParseError Text Error ‚Üí [Error.Report]
  finalizeError (M.TrivialError offset Nothing expected) =
    pure $
      DG.Err
        (Just "ParserUnexpectedToken")
        ("Unexpected token.")
        [(mkPos offset, DG.This $ printExpected expected)]
        []
  finalizeError (M.TrivialError offset (Just thing) expected) =
    pure $
      DG.Err
        (Just "ParserUnexpectedToken")
        ("Unexpected " <> prettyItem thing <> ".")
        [(mkPos offset, DG.This $ printExpected expected)]
        []
  finalizeError (M.FancyError offset components) =
    Set.toList components <&> \case
      M.ErrorCustom (Error _ custom) ‚Üí custom
      M.ErrorFail msg ‚Üí
        DG.Err
          (Just "ParserMonadFail")
          "The fail function got invoked while parsing."
          [(mkPos offset, DG.This $ PP.pretty msg)]
          [shouldNeverSeeThis]
      M.ErrorIndentation ordering expected actual ‚Üí
        DG.Err
          (Just "ParserIllegalIndentation")
          ( PP.hsep
              [ "Expected an indentation level of"
                  <> case ordering of
                    EQ ‚Üí ""
                    GT ‚Üí " more than"
                    LT ‚Üí " less than"
              , PP.pretty (M.unPos expected) <> ". Got"
              , PP.pretty $ M.unPos actual
              , "instead."
              ]
          )
          [ (iSpan actual, DG.This $ "Actual indentation")
          , (iSpan expected, DG.Where $ "Expected indentation")
          ]
          [shouldNeverSeeThis]
   where
    -- The span for an indentation error
    iSpan ‚à∑ M.Pos ‚Üí Base.Span
    iSpan pos =
      O.set (#end % O._2) (M.unPos pos + 1)
        . O.set (#begin % O._2) (M.unPos pos)
        $ mkPos offset

  shouldNeverSeeThis ‚à∑ DG.Note Error.Doc
  shouldNeverSeeThis = DG.Note "You should never see this in practice."

  mkPos ‚à∑ Int ‚Üí Base.Span
  mkPos offset = Base.mkMegaparsecSpan pos pos
   where
    pos =
      M.pstateSourcePos $
        M.reachOffsetNoLine offset $
          M.statePosState $
            M.initialState s i

  printExpected ‚à∑ Set (M.ErrorItem Char) ‚Üí Error.Doc
  printExpected variants
    | Set.size variants == 0 = "No idea what this is supposed to be..."
    | Set.size variants == 1 =
        PP.group $
          PP.sep
            [ "Expected"
            , maybeToMonoid
                . fmap prettyItem
                . viaNonEmpty head
                $ Set.toList variants
            , "."
            ]
    | otherwise =
        fold
          [ "Expected one of "
          , fold
              . intersperse ", "
              . fmap prettyItem
              $ Set.toList variants
          , "."
          ]

  prettyItem = PP.dquotes . PP.pretty . M.showErrorItem (Proxy @Text)

reportExpectedError ‚à∑ Error.Span ‚Üí Text ‚Üí Parser ()
reportExpectedError s l = do
  reportError
    ("Expected")
    (PP.hsep ["Unexpected token."])
    [
      ( s
      , DG.This $
          PP.hsep
            [ "I was supposed to find a"
            , PP.pretty l
            , "by now."
            ]
      )
    ]
    []

---------- Core combinators

-- | Turn a parser into one that keeps track of the source span it's consumed.
spanned ‚à∑ ‚àÄ a. Parser a ‚Üí Parser (Base.Span, a)
spanned p = do
  start ‚Üê M.getSourcePos
  inner ‚Üê p
  end ‚Üê M.getSourcePos
  pure (Base.mkMegaparsecSpan start end, inner)
{-# INLINE spanned #-}

-- | Turns a parser into one which keeps track of the source spam it's
-- consumed, and of the trivia that came before. The resulting parser will
-- consume spaces  and trivia that come after it, similarly to
-- 'Text.Megaparsec.Char.Lexer.lexeme' (this is also more performant, as it
-- means we don't have to keep parsing the same trivia over and over again).
--
-- | The resulting parser will also check whether its indentation matches
-- the expected amount.
token ‚à∑ ‚àÄ a. Parser a ‚Üí Parser (Base.Token a)
token p = do
  void $ checkIndentation True
  (innerSpan, inner) ‚Üê spanned p
  O.assign #relation IGT

  -- Take the saved trivia, and reset the list
  trivia ‚Üê O.use #trivia
  O.assign #trivia mempty
  sc

  pure $
    Base.Token
      { value = inner
      , trivia = trivia
      , span = innerSpan
      }

-- | Ensures the current indentation is at least the minimum allowed level.
checkIndentation ‚à∑ Bool ‚Üí Parser (Bool, M.Pos)
checkIndentation shouldReport = do
  col ‚Üê O.view #sourceColumn <$> M.getSourcePos
  indentation ‚Üê O.gview #indentation
  relation ‚Üê O.use #relation
  ok ‚Üê case relation of
    IEQ | col /= indentation ‚Üí do
      when shouldReport do
        M.registerFancyFailure . Set.singleton $
          M.ErrorIndentation EQ indentation col
      pure False
    IGT | col <= indentation ‚Üí do
      when shouldReport do
        M.registerFancyFailure . Set.singleton $
          M.ErrorIndentation GT indentation col
      pure False
    IGTE | col < indentation ‚Üí do
      -- TODO: this reporting is just wrong
      when shouldReport do
        M.registerFancyFailure . Set.singleton $
          M.ErrorIndentation GT indentation col
      pure False
    _ ‚Üí pure True
  pure (ok, col)

-- | Parse space characters / trivia. Although it might look at it, not all
-- results are thrown away. Parsed trivia are saved in the parser state.
sc ‚à∑ Parser ()
sc =
  M.skipMany . M.choice $
    [ M.hidden M.space1
    , M.hidden comment
    ]
{-# INLINE sc #-}

-- | Parse a comment, and save it to the parser's state. The comment will be
-- saved together with the next token inside the CST.
--
-- | Tokens at the end of the file will be saved together with the EOF token (see
-- 'Nihil.CST.Module.Module.eof').
comment ‚à∑ Parser ()
comment = do
  (triviapan, content) ‚Üê spanned (lineComment <|> blockComment)
  saveTrivia $ Base.TComment triviapan content
  pure ()

-- | Parse a line comment, returning its content. Specialized version of
-- 'Text.Megaparsec.Char.Lexer.skipLineComment.
lineComment ‚à∑ Parser Text
lineComment = do
  void $ M.string "--"
  M.takeWhileP (Just "character") (/= '\n')

-- | Parse a (possibly nested) block comment, returning its content.
-- Specialized version of 'Text.Megaparsec.Char.Lexer.skipBlockComment'.
blockComment ‚à∑ Parser Text
blockComment = do
  void $ M.string "{-"
  content ‚Üê M.manyTill chunk end
  pure $ fold content
 where
  chunk = blockComment <|> fmap Text.singleton M.anySingle
  end = M.string "-}"

-- | Here to go around Haskell's impredicative type issues.
newtype BlockMaker = BlockMaker (‚àÄ a. Parser a ‚Üí Parser a)

-- | Creates a parsers for a series of things that are aligned to eachother
-- horizontally. Examples include:
-- - pattern match branches
-- - statements in some block
-- - declarations in a module
mkBlock ‚à∑ Parser BlockMaker
mkBlock =
  checkIndentation True <&> \(_, expected) ‚Üí BlockMaker \p ‚Üí do
    O.assign #relation IEQ
    r ‚Üê withIndentation expected p
    O.assign #relation IGT
    pure r

-- | Similar to @mkBlock@, except returning @Nothing@ on indentation violations.
tryMkBlock ‚à∑ Parser (Maybe BlockMaker)
tryMkBlock =
  checkIndentation False <&> \(s, expected) ‚Üí
    guard s $> BlockMaker \p ‚Üí do
      O.assign #relation IEQ
      r ‚Üê withIndentation expected p
      O.assign #relation IGT
      pure r

blockLike ‚à∑ ‚àÄ a. BlockMaker ‚Üí Parser a ‚Üí Parser a
blockLike (BlockMaker f) a = f a

-- | Turns a parser into one with strict indentation enabled. Check
-- `ParserState.relation` for details regarding what this means.
exactlyIndented ‚à∑ ‚àÄ a. Parser a ‚Üí Parser a
exactlyIndented p =
  -- TODO: make this not report indentation errors
  mkBlock >>= flip blockLike p

restoreRelation ‚à∑ ‚àÄ a. Parser a ‚Üí Parser a
restoreRelation p = do
  r ‚Üê O.use #relation
  result ‚Üê p
  O.assign #relation r
  pure result

-- | Make a parser skip indentation checks (kinda...).
anyIndentation ‚à∑ ‚àÄ a. Parser a ‚Üí Parser a
anyIndentation p = restoreRelation do
  O.assign #relation IAny *> p

-- | Make a parser only work at the very start of the line.
unindented ‚à∑ ‚àÄ a. Parser a ‚Üí Parser a
unindented p = restoreRelation do
  O.assign #relation IEQ *> withIndentation (M.mkPos 1) p

-- | A parser which succeeds precisely when the indentation rules are violated.
badIndent ‚à∑ Parser ()
badIndent = do
  (ok, _) ‚Üê checkIndentation False
  when ok $ fail "bad indent"
{-# INLINE badIndent #-}

---------- Keyed parsers
data KeyedParser a = KeyedParser (Set Text) (Parser a)
  deriving (Generic, Functor)

keyedAppend ‚à∑ ‚àÄ a. LabelledParser a ‚Üí KeyedParser a ‚Üí KeyedParser a
keyedAppend (l, p) k@(KeyedParser ls ps)
  | Set.member l ls = k
  | otherwise = KeyedParser (Set.insert l ls) (ps <|> p)

---------- Labelled parsers

-- | Like @'Parser', but contains an additional label for errors.
type LabelledParser a = (Text, Parser a)

label ‚à∑ ‚àÄ a. Text ‚Üí Parser a ‚Üí LabelledParser a
label l p = (l, p)
{-# INLINE label #-}

---------- Error-tolerant parsing

-- | Consume text, annotating it as junk, until the given parser succeeds.
junkTill ‚à∑ ‚àÄ a. (Base.HasTrivia a) ‚áí LabelledParser a ‚Üí Parser a
junkTill (l, p) = do
  triviaAmount ‚Üê length <$> O.use #trivia
  (chunks, res) ‚Üê M.manyTill_ single p

  case nonEmpty chunks of
    Just neChunks ‚Üí do
      let chunksSpan = foldl1 Error.mergeSpans $ fst <$> neChunks
      let piece = Base.TJunk chunksSpan (foldMap snd chunks)
      reportError
        "Junk"
        ( PP.hsep
            [ "I was looking for a"
            , PP.pretty l
            , "when I came across a bunch of unreadable characters."
            ]
        )
        [(chunksSpan, DG.This "I have no idea what this is supposed to mean.")]
        []

      case Base.attachTrivia (pure piece) res of
        Just res' ‚Üí pure res'
        Nothing ‚Üí do
          O.modifying #trivia (Seq.insertAt triviaAmount piece)
          pure res
    Nothing ‚Üí pure res
 where
  single ‚à∑ Parser (Error.Span, Text)
  single = spanned chunk <* sc

  chunk ‚à∑ Parser Text
  chunk = do
    h ‚Üê M.anySingle
    if Char.isAlpha h
      then do
        more ‚Üê M.takeWhileP (Just "junk") Char.isAlpha
        pure $ Text.singleton h <> more
      else pure $ Text.singleton h

-- | Similar to 'junkTill', except will automatically stop when encountering
-- an element matching the context's 'stopOn' parser.
tryJunkTill ‚à∑ ‚àÄ a. (Base.HasTrivia a) ‚áí LabelledParser a ‚Üí Parser (Maybe a)
tryJunkTill (l, inner) = do
  (KeyedParser _ pre, KeyedParser _ post) ‚Üê O.gview #stopOn
  junkTill
    . (l,)
    . M.choice
    $ [ Nothing <$ M.lookAhead pre
      , Just <$> inner
      , Nothing <$ M.lookAhead post
      ]
{-# INLINE tryJunkTill #-}

-- | Similar to @'tryJunkTill', except an error gets reported if the value
-- is missing.
tryReportedJunkTill ‚à∑ ‚àÄ a. (Base.HasTrivia a) ‚áí LabelledParser a ‚Üí Parser (Maybe a)
tryReportedJunkTill p = do
  (resultSpan, result) ‚Üê spanned $ tryJunkTill p
  when (isNothing result) do
    reportExpectedError resultSpan $ fst p

  pure result

---------- Pretty printing
instance PP.Pretty ParserState where
  pretty st =
    Base.prettyTree
      "ParserState"
      [ PP.pretty ("relation" ‚à∑ Text, O.view #relation st)
      , PP.pretty ("trivia" ‚à∑ Text, toList $ O.view #trivia st)
      ]

instance PP.Pretty IndentationRelation where
  pretty i = PP.pretty (show i ‚à∑ Text)
